#compdef hyprmcsr

_hyprmcsr() {
  local -a commands profiles
  local config_root config_file download_root repo_dir jars_dir
  commands=(run destroy delete-old-worlds run-jar setup-audio-splitter update version)

  # config_root dynamisch bestimmen
  config_root="${XDG_CONFIG_HOME:-$HOME/.config}/hyprmcsr"
  config_file="$config_root/config.json"

  # Profile dynamisch ermitteln
  if [[ -d $config_root ]]; then
    profiles=(${(f)"$(ls $config_root/*.profile.json 2>/dev/null | xargs -n1 basename | sed 's/\.profile\.json$//')"})
  else
    profiles=()
  fi

  local context state state_descr line
  _arguments -C \
    '-h[Set global config profile]:profile:(${profiles})' \
    '-p[Set profile]: :_nothing' \
    '1:command:((run destroy delete-old-worlds run-jar setup-audio-splitter update version))' \
    '*::args:->args'

  case $state in
    args)
      # Ermittle das aktuelle Kommando (nach Optionen)
      local cmd_idx=1
      local cmd_name=""
      for ((i=1; i<=$#words; i++)); do
        if [[ ${words[i]} != -* ]]; then
          cmd_name=${words[i]}
          cmd_idx=$i
          break
        fi
      done
      case $cmd_name in
        delete-old-worlds)
          _arguments '1:regex:' '2:keep_n:'
          ;;
        run-jar)
          # Read JAR names from repositories.json
          local repositories_file="${XDG_CONFIG_HOME:-$HOME/.config}/hyprmcsr/repositories.json"
          local -a jar_names
          if [[ -f $repositories_file ]]; then
            jar_names=(${(f)"$(command jq -r '.jar | keys[]' "$repositories_file" 2>/dev/null)"})
          else
            jar_names=()
          fi
          if (( ${#jar_names[@]} )); then
            _values 'jar name' $jar_names
          else
            _message 'no jars configured in repositories.json'
          fi
          ;;
        "")
          # Wenn noch kein Kommando eingegeben wurde, Kommandos vorschlagen
          _values 'command' $commands
          ;;
      esac
      ;;
  esac
}

compdef _hyprmcsr hyprmcsr
